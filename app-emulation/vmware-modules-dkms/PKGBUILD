# Maintainer : Markus Kitsinger (SwooshyCueb) <root@swooshalicio.us>
# Contributor : Raansu <Gero3977 "at" gmail {dot} com>
# Contributor : adytzu2007 <adybac "at" gmail {dot} com>
# Contributor : Shaumux <shaumya "at" gmail {dot} com>
# Contributor : Alain Kalker <a {dot} c {dot} kalker "at" gmail {dot} com>
# Contributor : Willy Sudiarto Raharjo < willysr "at" slackware-id {dot} com>

# We should probably find something other than VMMON_VERSION to base pkgver on,
# as VMMON_VERSION doesn't seem to change much.

pkgname=vmware-modules-dkms
_pkgbase=vmware-modules
pkgver=308.0
pkgrel=21
pkgdesc="VMware Workstation kernel modules (DKMS)"
arch=('i686' 'x86_64')
url="http://www.vmware.com/"
license=('GPL2')
conflicts=('open-vm-tools-modules')

_vmware_location=/lib/vmware/modules/source

depends=('dkms')
optdepends=('linux-headers: needed to build the module')
options=('!buildflags')
install=${pkgname}.install
source=('Makefile'
        '60-vmware.rules'
        'dkms.conf.in'
        'NOTES')
sha256sums=('0d28080016f38708cd80c69ac9829d37f15f4cac23aa763bfe5a6ad75e96243b'
            '733172b5df23e96918f656ff3f566042f332455b5b7121621e626833e133b7c6'
            '489ab58c7bc605588271f41e563c4e97ea65798fbc4ab7ec5b2135c79df5de1a'
            'SKIP')
tmp_files=()

for patch_item in "patches"/*; do
	extension="${patch_item##*.}"
	if [ "${extension}" = "patch" ]; then
		cp "${patch_item}" .
		patch_name=$(basename "$patch_item")
		tmp_files+=("${patch_name}")
		source+=("${patch_name}")
		sha256sum_item=($(sha256sum ${patch_name}))
		sha256sums+=("${sha256sum_item}")
	fi
done

build()
{
    cd ${srcdir}

    # create dkms.conf
    sed -e "s/@PKGNAME@/$_pkgbase/; s/@PKGVER@/$pkgver/" < ${srcdir}/dkms.conf.in > ${srcdir}/dkms.conf
}

package()
{
    # make folder for dkms
    install -m755 -d ${pkgdir}/usr/src/${_pkgbase}-${pkgver}
    install -m755 -d $pkgdir/usr/lib/modules-load.d/

    install -D -m0644 ${srcdir}/dkms.conf ${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf
    install -D -m0644 ${srcdir}/Makefile ${pkgdir}/usr/src/${_pkgbase}-${pkgver}/Makefile
    install -D -m0644 ${srcdir}/60-vmware.rules ${pkgdir}/etc/udev/rules.d/60-vmware.rules
    
    echo -e "vmblock\nvmci\nvmmon\nvmnet\nvsock" > "$pkgdir/usr/lib/modules-load.d/vmware-modules.conf"

    for tmp_file in "${tmp_files[@]}"; do
	install -D -m0644 ${srcdir}/${tmp_file} ${pkgdir}/usr/src/${_pkgbase}-${pkgver}/${tmp_file}
    done

    for tmp_file in "${tmp_files[@]}"; do
		rm "../$tmp_file"
    done
}
