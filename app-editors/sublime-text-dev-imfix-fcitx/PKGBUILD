# Maintainer  : farseerfc <farseerfc@archlinuxcn.org>
# Contributor : Fernando "Firef0x" G.P. da Silva <firefgx { aT ) gmail [ d0t } com>
# Contributor : Sander Boom <sander at inflowmotion dot nl> (From sublime-text-dev)
# Contributor : realitygaps <realitygaps at yahoo dot com> (From sublime-text-dev)
# Contributor : ska <skatiger (at} gmail {dot) com> (From sublime-text-imfix)
# Contributor : Leon <leon860516 at gmail dot com> (From sublime-text-dev-imfix-fcitx)
pkgname=('sublime-text-dev-imfix-fcitx')
pkgver=3.3143
pkgrel=4
arch=('i686' 'x86_64')
url="http://www.sublimetext.com/3"
license=('custom')
depends=('desktop-file-utils' 'gtk2' 'libpng')
optdepends=('gksu: sudo-save support')
provides=("sublime-text-dev=${pkgver}" 'sublime-text-nightly')
conflicts=('sublime-text-dev' 'sublime-text-nightly')
options=('!strip')
changelog=README.md
pkgdesc="Sophisticated text editor for code, HTML and prose, development build with Fcitx input method support"

_pkgname=sublime_text_3

install=${_pkgname}.install

source=(
	"LICENSE"
)
source_i686=("https://download.sublimetext.com/${_pkgname}_build_${pkgver:2}_x32.tar.bz2")
source_x86_64=("http://download.sublimetext.com/${_pkgname}_build_${pkgver:2}_x64.tar.bz2")

sha512sums=(
	'54b356867e6699fe88130c183ec76ea631f4afeab1a15f37e9ecd460f726bc7c80f056fd57637c24a67b865522e9d3bb5156414fe3cc5d091be9e1f21ef0f31f'
)
sha512sums_i686=('471716815a171f786ad5f12b743ec3c17dcde2fc165522e523d338c9184af7789095e2de41e89d0c4b0dc58bfe409f116662016e7014c528650ce074c101084b')
sha512sums_x86_64=('db6375cbcaf1cb6e1d583aa043abeaeecf0236e1d2e23b965b6c2dd616ec2277d73786d2a9c273329c1a94890edc7ddce1551292b5170dc5b558cd7246c5093f')

build() {
	git clone https://github.com/lyfeyaj/sublime-text-imfix ${srcdir}/sublime-text-imfix
	cp "${srcdir}/sublime-text-imfix/src/subl" .
	cp "${srcdir}/sublime-text-imfix/src/sublime-text.desktop" .
	cp "${srcdir}/sublime-text-imfix/src/sublime-imfix.c" .
	gcc -shared sublime-imfix.c  -o libsublime-imfix.so $(pkg-config --libs --cflags gtk+-2.0) -fPIC
}

_package_common() {
	# Copy everything
	install -d "${pkgdir}/opt"
	cp --preserve=mode -r "${_pkgname}" "${pkgdir}/opt/sublime_text"

	# Install IM fix library
	install -Dm755 libsublime-imfix.so \
    	${pkgdir}/opt/sublime_text/libsublime-imfix.so

	# Install icons and desktop shortcuts
	for res in 16x16 32x32 48x48 128x128 256x256; do
		install -d "${pkgdir}/usr/share/icons/hicolor/${res}/apps"
		ln -sf "/opt/sublime_text/Icon/${res}/sublime-text.png" \
			"${pkgdir}/usr/share/icons/hicolor/${res}/apps/sublime-text.png"
	done

	install -d "${pkgdir}/usr/share/applications"
	install -Dm644 ${srcdir}/sublime-text.desktop \
		"${pkgdir}/usr/share/applications/${_pkgname}.desktop"

	# Install bin file
	install -Dm755 ${srcdir}/subl "${pkgdir}/usr/bin/subl"
  
	# Install license file
	install -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_sublime-text-dev-imfix-fcitx() {
	pkgdesc="Sophisticated text editor for code, HTML and prose, development build with Fcitx input method support"
	_package_common
}

# vim:set sts=2 sw=2 ts=2 et:
